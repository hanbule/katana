plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'org.ajoberstar.git-publish' version '2.1.1'

    id 'maven-publish'
}

group 'jp.katana'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation files("libs/jraknet-2.11.3.jar")

    implementation "com.nimbusds:nimbus-jose-jwt:7.3"

    implementation "org.iq80.leveldb:leveldb:0.12"

    implementation "com.google.code.gson:gson:2.8.5"
    implementation "org.yaml:snakeyaml:1.24"

    implementation "org.apache.logging.log4j:log4j-core:2.12.0"

    implementation "net.minecrell:terminalconsoleappender:1.1.1"

    implementation "org.jline:jline-terminal:3.11.0"
    implementation "org.jline:jline-terminal-jna:3.11.0"
    implementation "org.jline:jline-reader:3.11.0"

    implementation 'com.github.ben-manes.caffeine:caffeine:2.7.0'

    testImplementation "org.junit.jupiter:junit-jupiter:5.5.1"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Main-Class': 'jp.katana.server.Main'
    }
}

tasks.test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'jp.katana'
            artifactId = 'katana'
            version = '1.0-SNAPSHOT'

            from components.java
        }
    }

    repositories {
        maven {
            url = "$buildDir/repo"
        }
    }
}

gitPublish {
    repoUri = 'git@github.com:hiroki19990625/katana.git'
    referenceRepoUri = "$buildDir/repo"
    branch = 'gh-pages'
    repoDir = file("$buildDir/repo")
    preserve {
        include '**/*'
    }
    commitMessage = 'Publishing a new page'
}